#!/bin/tcsh

set configFile=""
set usage="Usage: $0 -c configFile [-h]"

set argv=`getopt "c:h" $*`

if ($status != 0) then
	echo "$usage"
	exit 1
endif

# parse arguments
while ("$1" != "")
	switch($1)
		case "-c":
			set configFile="$2"
			shift
			breaksw
		case "-h":
			echo "$usage"
			exit 1
	endsw
	shift
end

if ("$configFile" == "") then
	echo "$usage"
	exit 1
endif

# check for the config file
if (-e $configFile && -r $configFile) then
	source $configFile
else
	echo "$configFile does not exist and/or is not readable"
	exit 1
endif

# used for proper permissioning
umask 002

# script configuration
set date		= `date -d yesterday +"%Y %m %d"`
set lampDir		= "/data/cams/$cameraCode"
set imageDir	= "$lampDir/images/archive/$date[1]/$date[2]/$date[3]"
set compDir		= "$lampDir/composites/archive/$date[1]/$date[2]"
set movieDir	= "$lampDir/movies"
set scriptLog	= "/var/log/cams/$cameraCode.prune.log"

# create a header for the log file for this creation instance
echo "$cameraCode BEGIN PRUNE CONTENT `date +%c`" > $scriptLog

# delete empty images from hour folders
echo "    removing empty images from the archive $imageDir" >> $scriptLog
find $imageDir/*/* -type f -empty -exec rm {} \; >>& $scriptLog

# delete corrupted log files from the hour folders
echo "    removing corrupted log files from the archive $imageDir" >> $scriptLog
rm -vf $imageDir/*/.*.log.* >>& $scriptLog

# delete empty composites from month folders
echo "    removing empty composites from the archive $compDir" >> $scriptLog
find $compDir/* -type f -empty -exec rm {} \; >>& $scriptLog

# delete old movies
echo "    removing old copies of movies from $movieDir" >> $scriptLog
find $movieDir/${cameraCode}*.avi -mtime +10 -exec rm -f '{}' \; >>& $scriptLog

# create a footer for the log file for this creation instance
echo "$cameraCode END PRUNE CONTENT `date +%c`" >> $scriptLog
