#!/bin/tcsh

set configFile=""
set dateFlag=""
set usage="Usage: $0 [-d datestring] [-h]"

set argv=`getopt "d:h" $*`

if ($status != 0) then
	echo "$usage"
	exit 1
endif

# parse arguments
while ("$1" != "")
	switch($1)
		case "-d":
			set dateFlag="-d $2"
			shift
			breaksw
		case "-h":
			echo "$usage"
			exit 1
	endsw
	shift
end

# used for proper permissioning
umask 002

# script configuration
set date		= `date $dateFlag +"%Y %m %d"`
set movieDir	= "/data/www/remote_sensing/goes/movies"
set imageDir	= "/data/www/remote_sensing/goes/images/$date[1]/$date[2]/$date[3]"
set scriptLog	= "/var/log/cams/goes.movie.log"

# create a header for the log file for this creation instance
echo "GOES BEGIN FINAL MOVIE CREATION `date +%c`" > $scriptLog

# define which movie this is
set movieName	= "goes$date[1]$date[2]$date[3].avi"

# create a list of all the jpgs that are going to go into the movie
set fileList	= "/tmp/$movieName.movie.txt"
find $imageDir/*/$date[1]$date[2]$date[3]*.jpg | sort > $fileList

# create the movie
echo "    creating /tmp/$movieName `date +%c`" >> $scriptLog
mencoder mf://@$fileList -mf fps=10 -ovc lavc -lavcopts vcodec=mjpeg:vbitrate=1000 -o /tmp/$movieName >>& $scriptLog

# move the movie to the web dir for usage
echo "    moving /tmp/$movieName to $movieDir/$movieName" >> $scriptLog
cp /tmp/$movieName $movieDir/$movieName >>& $scriptLog

# delete the temp movie
rm -f $fileList  >>& $scriptLog
rm -f /tmp/$movieName >>& $scriptLog

# create a footer for the log file for this creation instance
echo "GOES END FINAL MOVIE CREATION `date +%c`" >> $scriptLog
