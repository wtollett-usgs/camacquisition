#!/bin/tcsh

set configFile=""
set dateFlag=""
set usage="Usage: $0 -c configfile [-d datestring] [-h]"

set argv=`getopt "c:d:h" $*`

if ($status != 0) then
	echo "$usage"
	exit 1
endif

# parse arguments
while ("$1" != "")
	switch($1)
		case "-c":
			set configFile="$2"
			shift
			breaksw
		case "-d":
			set dateFlag="-d $2"
			shift
			breaksw
		case "-h":
			echo "$usage"
			exit 1
	endsw
	shift
end

if ("$configFile" == "") then
	echo "$usage"
	exit 1
endif

# check for the config file
if (-e $configFile && -r $configFile) then
	source $configFile
else
	echo "$configFile does not exist and/or is not readable"
	exit 1
endif

# used for proper permissioning
umask 002

# script configuration
set date		= `date $dateFlag +"%Y %m %d"`
set movieDir	= "/data/cams/$cameraCode/movies"
set imageDir	= "/data/cams/$cameraCode/images/archive/$date[1]/$date[2]/$date[3]"
set scriptLog	= "/var/log/cams/$cameraCode.movie.log"

# create a header for the log file for this creation instance
echo "$cameraCode BEGIN FINAL MOVIE CREATION `date +%c`" > $scriptLog

# add the PAN frame to the name array 
if ($#name > 1) then
	set name	= ($name "PAN")
endif

# start at the first camera angle
@ i = 1

while ($i < $#name + 1)

	# define which movie this is
	set movieName	= "$cameraCode$date[1]$date[2]$date[3]$name[$i].avi"

	# create a list of all the jpgs that are going to go into the movie
	set fileList	= "/tmp/$movieName.movie.txt"
	if ($name[$i] == "PAN") then
		find $imageDir/*/????????????$name[$i].jpg | sort > $fileList
	else 
		find $imageDir/*/??????????????$name[$i].jpg | sort > $fileList
	endif

	# create the movie
	echo "    creating /tmp/$movieName `date +%c`" >> $scriptLog
	mencoder mf://@$fileList -mf fps=10 -ovc lavc -lavcopts vcodec=mpeg4:vbitrate=3840000 -o /tmp/$movieName >>& $scriptLog

	# move the movie to the web dir for usage
	echo "    moving /tmp/$movieName to $movieDir/$movieName" >> $scriptLog
	cp /tmp/$movieName $movieDir/$movieName >>& $scriptLog

	# delete the temp movie
	rm -f $fileList  >>& $scriptLog
	rm -f /tmp/$movieName >>& $scriptLog

	# goto the next camera angle
	@ i++

end

# create a footer for the log file for this creation instance
echo "$cameraCode END FINAL MOVIE CREATION `date +%c`" >> $scriptLog
